package com.dslconsultancy.uiscript.generator

import com.dslconsultancy.uiscript.components.ComponentDefinition
import com.dslconsultancy.uiscript.structural.UiModule
import com.google.inject.Inject

class UiModuleTemplates {

	@Inject extension ParametrisablesTemplates
	@Inject extension TypeDefinitionsTemplates

	def generateTsxForUiModule(UiModule it) {
		'''
		// Generated by UIScript - do not modify!

		«IF !typeDefinitions.empty»
			import {observable} from "mobx";
		«ENDIF»
		«IF !parametrisables.filter(ComponentDefinition).empty»
			import {observer} from "mobx-react";
			import * as React from "react";
		«ENDIF»


		«IF !typeDefinitions.empty»
			// type definitions:

			«FOR typeDefinition : typeDefinitions»
				«typeDefinition.asTs»

			«ENDFOR»


		«ENDIF»
		«IF !parametrisables.empty»
			// parametrisables:

			«FOR parametrisable : parametrisables»
				«parametrisable.asTsx»

			«ENDFOR»
		«ENDIF»
		'''
	}
	
}
