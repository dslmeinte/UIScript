@Ecore(nsURI="http://www.dslconsultancy.com/uicript/types")
@GenModel(forceOverwrite="true", modelName="UITypes", updateClasspath="false", complianceLevel="5.0")
package com.dslconsultancy.uiscript.types

import com.dslconsultancy.uiscript.core.Named


abstract class DefinedType extends Named {}


class Structure extends DefinedType {
	boolean persistent
	contains Feature<TypeLiteral>[] features
}

class Feature<T extends TypeLiteral> extends Named {
	boolean optional
	contains T ^type
}

class Enumeration extends DefinedType {
	contains EnumerationLiteral[] literals
}

class EnumerationLiteral extends Named {
	String displayName
}


abstract class TypeLiteral {}

class BuiltinTypeLiteral extends TypeLiteral {
	BuiltinTypes builtin
}

enum BuiltinTypes {
	string as "String"
	boolean as "Boolean" = 1
	integer as "Integer" = 2
	url as "URL" = 3
	text as "Text" = 4
	number as "Number" = 5
	email as "Email" = 6
	phone as "Phone" = 7
	date as "Date" = 8
	password as "Password" = 9
	// TODO  add DateTime (for timestamps and such)?
}

class DefinedTypeLiteral extends TypeLiteral {
	refers DefinedType ^type
}

class ListTypeLiteral extends TypeLiteral {
	contains TypeLiteral itemType
}


abstract class SyntheticTypeLiteral extends TypeLiteral {}

class CallbackLiteral extends SyntheticTypeLiteral {}

class CallbackErrorResponseLiteral extends SyntheticTypeLiteral {}

class VoidLiteral extends SyntheticTypeLiteral {}

